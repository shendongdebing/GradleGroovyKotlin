apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 25
    buildToolsVersion "23.0.3"

    signingConfigs {
        myConfig {
            storeFile file("Depth.jks")
            keyAlias "ice"
            keyPassword "123456"
            storePassword "123456"
        }
    }

    defaultConfig {
        applicationId "com.ice.gradlegroovysample"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "0.0.1"
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            versionNameSuffix "-debug"
            signingConfig signingConfigs.myConfig
        }

        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.myConfig
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            def fileName
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                if (variant.buildType.name == 'release') {
                    fileName = "${projectName()}-${variant.buildType.name}-${defaultConfig.versionName}-${releaseTime()}.apk"
                } else if (variant.buildType.name == 'debug') {
                    fileName = "${projectName()}-${variant.buildType.name}-${defaultConfig.versionName}.apk"
                }
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("UTC"))
}

def projectName() {
    return getProject().getName();
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile "org.jetbrains.anko:anko-sdk15:$anko_version"

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.loopj.android:android-async-http:1.4.9'
}
repositories {
    mavenCentral()
}
